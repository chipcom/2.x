#include "inkey.ch"
#include "..\..\_mylib_hbt\function.ch"
#include "..\..\_mylib_hbt\edit_spr.ch"
#include "..\chip_mo.ch"


***** 27.01.21 определить коэф-т сложности лечения пациента и пересчитать цену
Function f_cena_kslp(/*@*/_cena,_lshifr,_date_r,_n_data,_k_data,lkslp,arr_usl,lPROFIL_K,arr_diag,lpar_org,lad_cr)
  Static s_1_may := 0d20160430, s_18 := 0d20171231, s_19 := 0d20181231
  static s_20 := 0d20201231
  Static s_kslp17 := {;
    {1,1.1, 0,  3},;   // до 4 лет
    {2,1.1,75,999};    // 75 лет и старше
   }
  Static s_kslp16 := {;
    {1,1.1 , 0,  3},;   // до 4 лет
    {2,1.05,75,999};    // 75 лет и старше
   }
  Local i, j, vksg, y := 0, fl, ausl := {}, s_kslp, _akslp := {}, sop_diag
  local countDays := _k_data - _n_data // кол-во дней лечения
  
  DEFAULT lad_cr TO space(10)

  _lshifr := alltrim(_lshifr) // перенес

  if _k_data > s_20
    if empty(lkslp)
      return _akslp
    endif
      // {code, Коэффициент, возрастНачало, возрастКонец}
      s_kslp := {;
      { 1, 1.02, 75, 999},; // Сложность лечения пациента, связанная с возрастом (лица 75 лет и старше) (в том числе, включая консультацию врача-гериатра)
      { 3, 1.20,  0,  18},; // Предоставление спального места и питания законному представителю (дети до 4 лет, дети старше 4 лет при наличии медицинских показаний)
      { 4, 1.20,  0,  18},; // Проведение первой иммунизации против респираторно-синцитиальной вирусной инфекции в период госпитализации по поводу лечения нарушений, возникающих в перинатальном периоде, являющихся показанием к иммунизации
      { 5, 1.20,  0, 999},; // Развертывание индивидуального поста
      { 6, 1.30,  0, 999},; // Проведение сочетанных хирургических вмешательств
      { 7, 1.30,  0, 999},; // Проведение однотипных операций на парных органах
      { 8, 1.50,  0, 999},; // Проведение антимикробной терапии инфекций, вызванных полирезистентными микроорганизмами
      { 9, 1.50,  0, 999},; // Наличие у пациента тяжелой сопутствующей патологии, осложнений заболеваний, сопутствующих заболеваний, влияющих на сложность лечения пациента (перечень указанных заболеваний и состояний
      {10, 1.50,  0, 999};  // Сверхдлительные сроки госпитализации, обусловленные медицинскими показаниями
    }

    // п.3 инструкции
    // Возраст пациента определяется на момент поступления на стационарное лечение.
    // Все случаи применения КСЛП (за исключением КСЛП1) подвергаются экспертному контролю.

    count_ymd(_date_r,_n_data,@y)
    lkslp := list2arr(lkslp)

    // for j := 1 to len(lkslp)
    //   if (i := ascan(s_kslp, {|x| x[1] == lkslp[j]})) > 0 // стоит данный КСЛП в выбранной КСГ
    //     if between(y,s_kslp[i,3],s_kslp[i,4])
    //       fl := .t.
    //       if lkslp[j] == 1  // для людей старше 75 лет КСЛП = 1
    //         fl := (lprofil_k != 16 ; // пациент лежит не на геронтологической койке
    //                 .and. !(_lshifr == "st38.001"))
    //       // elseif lkslp[j] == 5
    //       //   sop_diag := aclone(arr_diag)
    //       //   del_array(sop_diag,1)
    //       //   fl := (lprofil_k == 16 .and. ; // пациент лежит на геронтологической койке
    //       //           !(_lshifr == "st38.001") .and. ;//!(alltrim(arr_diag[1]) == "R54") .and. ; // с основным диагнозом не <R54-старость>
    //       //           ascan(sop_diag, {|x| alltrim(x) == "R54"}) > 0 ) // в соп.диагнозах есть <R54-старость>
    //       endif
    //       if fl
    //         aadd(_akslp,s_kslp[i,1])
    //         aadd(_akslp,s_kslp[i,2])
    //         exit
    //       endif
    //     endif
    //   endif
    // next

    // КСЛП=1 пациенты старше 75 лет
    if ascan(lkslp,1) > 0 .and. between(y,s_kslp[1,3],s_kslp[1,4])
      if (lprofil_k != 16 .and. ! (_lshifr == "st38.001"))
        // пациент лежит не на геронтологической койке 
        aadd(_akslp,1)
        aadd(_akslp,1.02)
      endif
    endif

    // КСЛП=3 спальное место законному представителю
    // НУЖЕН ЗАПРОС
    if ascan(lkslp,3) > 0 .and. between(y,s_kslp[2,3],s_kslp[2,4])
      // пункт 3.1.1
      // Предоставление спального места и питания законному представителю, при возрасте 
      // ребенка старше 4 лет, осуществляется при наличии медицинских показаний и 
      // оформляется протоколом врачебной комиссии с обязательным указанием в первичной 
      // медицинской документации.
      aadd(_akslp,3)
      aadd(_akslp,1.2)
    endif

    // КСЛП=4 иммунизация РСВ
    // НУЖЕН ЗАПРОС
    if ascan(lkslp,4) > 0
      // пункт 3.1.2
      // КСЛП применяется в случаях если сроки проведения первой иммунизации против 
      // респираторно-синцитиальной вирусной (РСВ) инфекции совпадают по времени с 
      // госпитализацией по поводу лечения нарушений, возникающих в перинатальном 
      // периоде, являющихся показанием к иммунизации.
      aadd(_akslp,4)
      aadd(_akslp,1.2)
    endif

    // КСЛП=5 развертывание индивидуального поста
    // НУЖЕН ЗАПРОС
    if ascan(lkslp,5) > 0
      aadd(_akslp,5)
      aadd(_akslp,1.2)
    endif

    // КСЛП=6 сочетанные хирургические операции
    if ascan(lkslp,6) > 0
      // пункт 3.1.3
      // Перечень сочетанных (симультанных) хирургических вмешательств, выполняемых во 
      // время одной госпитализации, представлен в таблице:        
      aadd(_akslp,6)
      aadd(_akslp,1.3)
    endif

    // КСЛП=7 парные органы и введены парные органы
    if ascan(lkslp,7) > 0 .and. lpar_org > 1
      // пункт 3.1.4
      // К данным операциям целесообразно относить операции на парных органах/частях тела,
      // при выполнении которых необходимы, в том числе дорогостоящие расходные материалы.
      // Перечень хирургических вмешательств, при проведении которых одновременно на двух
      // парных органах может быть применен КСЛП, представлен в таблице:
      aadd(_akslp,7)
      aadd(_akslp,1.3)
    endif

    // КСЛП = 8 антимикробная терапия
    // НУЖЕН ЗАПРОС
    if ascan(lkslp,8) > 0
      // пункт 3.1.5
      // В случаях лечения пациентов в стационарных условиях при заболеваниях и их 
      // осложнениях, вызванных микроорганизмами с антибиотикорезистентностью, а также 
      // в случаях лечения по поводу инвазивных микозов применяется КСЛП в соответствии 
      // со всеми перечисленными критериями:
      //  1) наличие инфекционного диагноза с кодом МКБ 10, вынесенного в клинический 
      //    диагноз (столбец Расшифровки групп ?Основной диагноз? или ?Диагноз осложнения?);
      //  2) наличие результатов микробиологического исследования с определением 
      //    чувствительности выделенных микроорганизмов к антибактериальным препаратам 
      //    и/или детекции основных классов карбапенемаз (сериновые, металлобеталактамазы),
      //    подтверждающих обоснованность назначения схемы антибактериальной терапии 
      //    (предполагается наличие результатов на момент завершения случая госпитализации, 
      //    в том числе прерванного);
      //  3) применение как минимум одного лекарственного препарата в парентеральной форме 
      //    из перечня МНН в составе схем антибактериальной и/или антимикотической терапии 
      //    в течение не менее чем 5 суток:        
      aadd(_akslp,8)
      aadd(_akslp,1.5)
    endif

    // КСЛП=9 тяжелые сопутствующие патологии
    if ascan(lkslp,9) > 0 .and. conditionKSLP_9_21(arr_diag)
      aadd(_akslp,9)
      aadd(_akslp,1.5)
    endif

    // КСЛП = 10 сверхдлительное лечение
    if ascan(lkslp,10) > 0 .and. conditionKSLP_10_21(countDays, _lshifr)
      aadd(_akslp,10)
      aadd(_akslp,1.5)
    endif

    // установим цену с учетом КСЛП
    if !empty(_akslp)
      _cena := round_5(_cena*ret_koef_kslp_21(_akslp),0)  // с 2019 года цена округляется до рублей
    endif

  elseif _k_data > s_19  // с 2019 года
    if !empty(lkslp)
      // _lshifr := alltrim(_lshifr)
      if _lshifr == "ds02.005" // ЭКО, lkslp = 12,13,14
        s_kslp := {;
          {12,0.60},;
          {13,1.10},;
          {14,0.19};
        }
        for i := 1 to len(arr_usl)
          if valtype(arr_usl[i]) == "A"
            aadd(ausl,alltrim(arr_usl[i,1]))  // массив многомерный
          else
            aadd(ausl,alltrim(arr_usl[i]))    // массив одномерный
          endif
        next i
        j := 0 // КСЛП - 1 схема
        if ascan(ausl,"A11.20.031") > 0  // крио
          j := 13  // 6 схема
          if ascan(ausl,"A11.20.028") > 0 // третий этап
            j := 0   // 2 схема
          endif
        elseif ascan(ausl,"A11.20.025.001") > 0  // первый этап
          j := 12  // 3 схема
          if ascan(ausl,"A11.20.036") > 0  // завершающий второй этап
            j := 12  // 4 схема
          elseif ascan(ausl,"A11.20.028") > 0  // завершающий третий этап
            j := 12  // 5 схема
          endif
        elseif ascan(ausl,"A11.20.030.001") > 0  // только четвертый этап
          j := 14  // 7 схема
        endif
        if (i := ascan(s_kslp, {|x| x[1] == j})) > 0
          aadd(_akslp,s_kslp[i,1])
          aadd(_akslp,s_kslp[i,2])
          _cena := round_5(_cena*s_kslp[i,2],0)  // с 2019 года цена округляется до рублей
        endif
        if !empty(_akslp) .and. _k_data > 0d20191231 // с 2020 года
          _akslp[1] += 3 // т.е. с 2020 года КСЛП для ЭКО 15,16,17
        endif
      else // остальные КСГ
        s_kslp := {;
          { 1,1.10, 0,  0},;  // до 1 года
          { 2,1.10, 1,  3},;  // от 1 до 3 лет включительно
          { 4,1.02,75,999},;  // 75 и старше
          { 5,1.10,60,999};   // 60 и старше и астения
        }
        count_ymd(_date_r,_n_data,@y)
        lkslp := list2arr(lkslp)
        for j := 1 to len(lkslp)
          if (i := ascan(s_kslp, {|x| x[1] == lkslp[j]})) > 0 // стоит данный КСЛП в выбранной КСГ
            if between(y,s_kslp[i,3],s_kslp[i,4])
              fl := .t.
              if lkslp[j] == 4
                fl := (lprofil_k != 16 ; // пациент лежит не на геронтологической койке
                        .and. !(_lshifr == "st38.001"))
              elseif lkslp[j] == 5
                sop_diag := aclone(arr_diag)
                del_array(sop_diag,1)
                fl := (lprofil_k == 16 .and. ; // пациент лежит на геронтологической койке
                        !(_lshifr == "st38.001") .and. ;//!(alltrim(arr_diag[1]) == "R54") .and. ; // с основным диагнозом не <R54-старость>
                        ascan(sop_diag, {|x| alltrim(x) == "R54"}) > 0 ) // в соп.диагнозах есть <R54-старость>
              endif
              if fl
                aadd(_akslp,s_kslp[i,1])
                aadd(_akslp,s_kslp[i,2])
                exit
              endif
            endif
          endif
        next
        if ascan(lkslp,11) > 0 .and. lpar_org > 1 // разрешена КСЛП=11 и введены парные органы
          aadd(_akslp,11)
          aadd(_akslp,1.2)
        endif
        if ascan(lkslp,18) > 0 .and. "cr6" $ lad_cr // разрешена КСЛП=18 и для сложного COVID-19
          aadd(_akslp,18)
          aadd(_akslp,1.2)
        endif
        if !empty(_akslp)
          _cena := round_5(_cena*ret_koef_kslp(_akslp),0)  // с 2019 года цена округляется до рублей
        endif
      endif
    endif
  elseif _k_data > s_18  // с 2018 года
    if !empty(lkslp)
      // _lshifr := alltrim(_lshifr)
      if _lshifr == "2005.0" // ЭКО, lkslp = 12,13,14
        s_kslp := {;
          {12,0.60},;
          {13,1.10},;
          {14,0.19};
        }
        for i := 1 to len(arr_usl)
          if valtype(arr_usl[i]) == "A"
            aadd(ausl,alltrim(arr_usl[i,1]))  // массив многомерный
          else
            aadd(ausl,alltrim(arr_usl[i]))    // массив одномерный
          endif
        next i
        j := 0 // КСЛП - 1 схема
        if ascan(ausl,"A11.20.031") > 0  // крио
          j := 13  // 6 схема
          if ascan(ausl,"A11.20.028") > 0 // третий этап
            j := 0   // 2 схема
          endif
        elseif ascan(ausl,"A11.20.025.001") > 0  // первый этап
          j := 12  // 3 схема
          if ascan(ausl,"A11.20.036") > 0  // завершающий второй этап
            j := 12  // 4 схема
          elseif ascan(ausl,"A11.20.028") > 0  // завершающий третий этап
            j := 12  // 5 схема
          endif
        elseif ascan(ausl,"A11.20.030.001") > 0  // только четвертый этап
          j := 14  // 7 схема
        endif
        if (i := ascan(s_kslp, {|x| x[1] == j})) > 0
          aadd(_akslp,s_kslp[i,1])
          aadd(_akslp,s_kslp[i,2])
          _cena := round_5(_cena*s_kslp[i,2],1)
        endif
      else // остальные КСГ
        s_kslp := {;
          { 1,1.10, 0,  0},;  // до 1 года
          { 2,1.10, 1,  3},;  // от 1 до 3 лет включительно
          { 4,1.05,75,999},;  // 75 и старше
          { 5,1.10,60,999};   // 60 и старше и астения
        }
        count_ymd(_date_r,_n_data,@y)
        lkslp := list2arr(lkslp)
        for j := 1 to len(lkslp)
          if (i := ascan(s_kslp, {|x| x[1] == lkslp[j]})) > 0
            if between(i,1,5) .and. between(y,s_kslp[i,3],s_kslp[i,4])
              aadd(_akslp,s_kslp[i,1])
              aadd(_akslp,s_kslp[i,2])
              _cena := round_5(_cena*s_kslp[i,2],1)
              exit
            endif
          endif
        next
      endif
    endif
  elseif _k_data > s_1_may ;                 // с 1 мая 2016 года
              .and. left(_lshifr,1) == '1' ; // круглосуточный стационар
              .and. !("." $ _lshifr)         // это шифр КСГ
    // _lshifr := alltrim(_lshifr)
    count_ymd(_date_r,_n_data,@y)
    vksg := int(val(right(_lshifr,3))) // последние три цифры - код КСГ
    if (fl := vksg < 900) // не диализ
      if year(_k_data) > 2016
        s_kslp := s_kslp17
        if y < 1 .and. between(vksg,105,111) // до 1 года и малая масса при рождении
          fl := .f.
        endif
      else
        s_kslp := s_kslp16
      endif
      if fl
        for i := 1 to len(s_kslp)
          if between(y,s_kslp[i,3],s_kslp[i,4])
            aadd(_akslp,s_kslp[i,1])
            aadd(_akslp,s_kslp[i,2])
            _cena := round_5(_cena*s_kslp[i,2],1)
            exit
          endif
        next
      endif
    endif
  endif
  return _akslp
  
***** 26.01.21 вернуть итоговый КСЛП для 2021 года
Function ret_koef_kslp_21(akslp)
  Local k := 1  // КСЛП равен 1

  if valtype(akslp) == "A" .and. len(akslp) >= 2
    for i := 1 TO len(akslp) STEP 2
      if i == 1
        k := akslp[2]
      else
        k += (akslp[i + 1] - 1)
      endif
    next
  endif
  if k > 1.8
    k := 1.8  // согласно п.3 инструкции
  endif
  return k

***** 26.01.21 проверка услувия для применения КСЛП=10 для 2021 года
function conditionKSLP_10_21(duration, lshifr)
  // duration - продолжительность лечения в днях
  // lshifr - код услуги КСГ
  //
  // пункт 3.1.7
  // Правила отнесения случаев к сверхдлительным не распространяются на КСГ, 
  // объединяющие случаи проведения лучевой терапии, в том числе в сочетании 
  // с лекарственной терапией (st19.075-st19.089, ds19.050-ds19.062), 
  // т.е. указанные случаи не могут считаться сверхдлительными и оплачиваться 
  // с применением КСЛП10.
  //
  // услуги КСГ исключений для КСЛП 10
  local exclKSG := {"st19.075", "st19.076", "st19.077", "st19.078", "st19.079", ;
    "st19.080", "st19.081", "st19.082", "st19.083", "st19.084", "st19.085", ;
    "st19.086", "st19.087", "st19.088", "st19.089",; 
    "ds19.050", "ds19.051", "ds19.052", "ds19.053", "ds19.054", "ds19.055", ;
    "ds19.056", "ds19.057", "ds19.058", "ds19.059", "ds19.060", "ds19.061", ;
    "ds19.062" }
  local fl := .f.

  if ( duration > 70 ) .and. ( ascan(exclKSG, lshifr) == 0 )
    fl := .t.
  endif
  return fl

  ***** 26.01.21 проверка услувия для применения КСЛП=9 для 2021 года
function conditionKSLP_9_21(arr_diag)
  // arr_diag - массив диагнозов, 1 элемент - основной диагноз,
  //   остальные сопутствующие и осложнения
  //
  // диагнозы влияющие на КСЛП 9
  // пункт 3.1.6
  //  К таким сопутствующим заболеваниям и осложнениям заболеваний относятся:
  //    ? Сахарный диабет типа 1 и 2 (E10.0-E10.9; E11.0-E11.9);
  //    ? Заболевания, включенные в Перечень редких (орфанных) заболеваний, 
  //      размещенный на официальном сайте Министерства здравоохранения РФ1;
  //    ? Рассеянный склероз (G35);
  //    ? Хронический лимфоцитарный лейкоз (C91.1);
  //    ? Состояния после трансплантации органов и (или) тканей 
  //      (Z94.0; Z94.1; Z94.4; Z94.8);
  //    ? Детский церебральный паралич (G80.0-G80.9);
  //    ? ВИЧ/СПИД, стадии 4Б и 4В, взрослые (B20 ? B24);
  //    ? Перинатальный контакт по ВИЧ-инфекции, дети (Z20.6).
  // При применении КСЛП9 в обязательном порядке в первичной медицинской 
  // документации отражаются мероприятия проводимые по вопросу лечения 
  // вышеуказанной тяжелой сопутствующей патологии (например: дополнительные 
  // лечебно-диагностические мероприятия, назначение лекарственных препаратов, 
  // увеличение срока госпитализации и т.д.), которые отражают дополнительные 
  // затраты медицинской организации на лечение данного пациента. 
  local diag, i := 0, tmp
  local inclDIAG := {;
    "E10.0", "E10.1", "E10.2", "E10.3", "E10.4", "E10.5", "E10.6", "E10.7", "E10.8", "E10.9", ;
    "E11.0", "E11.1", "E11.2", "E11.3", "E11.4", "E11.5", "E11.6", "E11.7", "E11.8", "E11.9", ;
    "G35", "C91.1", "Z94.0", "Z94.1", "Z94.4", "Z94.8", ;
    "G80.0", "G80.1", "G80.2", "G80.3", "G80.4", "G80.8", "G80.9", ;
    "B20", "B21", "B22", "B23", "B24", ;
    "Z20.6";
  }
  local fl := .f.

  if len( arr_diag ) != 1 // кроме основного есть и другие диагнозы
    //  .and. ( ascan(exclKSG, lshifr) == 0 )
  endif

  for each diag in arr_diag
    i++
    if i == 1
      loop
    endif
    if upper(substr(diag,1,1)) == 'B' // что-то с ВИЧ
      tmp := upper(substr(diag,1,3))
    else
      tmp := upper(diag)
    endif
    if ascan(inclDIAG, diag) > 0
      fl := .t.
      exit
    endif
  next

  return fl